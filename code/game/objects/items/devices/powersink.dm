// Powersink - used to drain station power

/obj/item/powersink
	name = "power sink"
	desc = "A nulling power sink which drains energy from electrical systems."
	icon = 'icons/obj/device.dmi'
	icon_state = "powersink0"
	item_state = "electronic"
	w_class = WEIGHT_CLASS_BULKY
	flags = CONDUCT
	throwforce = 5
	throw_speed = 1
	throw_range = 2
	materials = list(MAT_METAL=750)
	origin_tech = "powerstorage=5;syndicate=5"
	var/drain_rate = 1600000		// amount of power to drain per tick
	var/apc_drain_rate = 50 		// Max. amount drained from single APC. In Watts.
	var/dissipation_rate = 20000	// Passive dissipation of drained power. In Watts.
	var/power_drained = 0 			// Amount of power drained.
	var/max_power = 1e10			// Detonation point.
	var/mode = 0					// 0 = off, 1=clamped (off), 2=operating
	var/drained_this_tick = 0		// This is unfortunately necessary to ensure we process powersinks BEFORE other machinery such as APCs.
	var/admins_warned = 0			// stop spam, only warn the admins once that we are about to go boom

	var/datum/powernet/PN			// Our powernet
	var/obj/structure/cable/attached		// the attached cable

/obj/item/powersink/Destroy()
	processing_objects.Remove(src)
	GLOB.processing_power_items.Remove(src)
	PN = null
	attached = null
	return ..()

/obj/item/powersink/attackby(var/obj/item/I, var/mob/user)
	if(istype(I, /obj/item/screwdriver))
		if(mode == 0)
			var/turf/T = loc
			if(isturf(T) && !T.intact)
				attached = locate() in T
				if(!attached)
					to_chat(user, "No exposed cable here to attach to.")
					return
				else
					anchored = 1
					mode = 1
					src.visible_message("<span class='notice'>[user] attaches [src] to the cable!</span>")
					message_admins("Power sink activated by [key_name_admin(user)] at ([x],[y],[z] - <A HREF='?_src_=holder;adminplayerobservecoodjump=1;X=[x];Y=[y];Z=[z]'>JMP</a>)")
					log_game("Power sink activated by [key_name(user)] at ([x],[y],[z])")
					return
			else
				to_chat(user, "Device must be placed over an exposed cable to attach to it.")
				return
		else
			if(mode == 2)
				processing_objects.Remove(src) // Now the power sink actually stops draining the station's power if you unhook it. --NeoFite
				GLOB.processing_power_items.Remove(src)
			anchored = 0
			mode = 0
			src.visible_message("<span class='notice'>[user] detaches [src] from the cable!</span>")
			set_light(0)
			icon_state = "powersink0"

			return
	else
		..()

/obj/item/powersink/attack_ai()
	return

/obj/item/powersink/attack_hand(var/mob/user)
	switch(mode)
		if(0)
			..()
		if(1)
			src.visible_message("<span class='notice'>[user] activates [src]!</span>")
			mode = 2
			icon_state = "powersink1"
			processing_objects.Add(src)
			GLOB.processing_power_items.Add(src)
		if(2)  //This switch option wasn't originally included. It exists now. --NeoFite
			src.visible_message("<span class='notice'>[user] deactivates [src]!</span>")
			mode = 1
			set_light(0)
			icon_state = "powersink0"
			processing_objects.Remove(src)
			GLOB.processing_power_items.Remove(src)

/obj/item/powersink/pwr_drain()
	if(!attached)
		return 0

	if(drained_this_tick)
		return 1
	drained_this_tick = 1

	var/drained = 0

	if(!PN)
		return 1

	set_light(12)
	PN.trigger_warning()
	// found a powernet, so drain up to max power from it
	drained = PN.draw_power(drain_rate)
	// if tried to drain more than available on powernet
	// now look for APCs and drain their cells
	if(drained < drain_rate)
		for(var/obj/machinery/power/terminal/T in PN.nodes)
			// Enough power drained this tick, no need to torture more APCs
			if(drained >= drain_rate)
				break
			if(istype(T.master, /obj/machinery/power/apc))
				var/obj/machinery/power/apc/A = T.master
				if(A.operating && A.cell)
					A.cell.charge = max(0, A.cell.charge - apc_drain_rate)
					drained += apc_drain_rate
					if(A.charging == 2) // If the cell was full
						A.charging = 1 // It's no longer full
	power_drained += drained
	return 1


/obj/item/powersink/process()
	drained_this_tick = 0
	power_drained -= min(dissipation_rate, power_drained)
	if(power_drained > max_power * 0.98)
		if(!admins_warned)
			admins_warned = 1
			message_admins("Power sink at ([x],[y],[z] - <A HREF='?_src_=holder;adminplayerobservecoodjump=1;X=[x];Y=[y];Z=[z]'>JMP</a>) is 95% full. Explosion imminent.")
		playsound(src, 'sound/effects/screech.ogg', 100, 1, 1)
	if(power_drained >= max_power)
		explosion(src.loc, 4,8,16,32)
		qdel(src)
		return
	if(attached && attached.powernet)
		PN = attached.powernet
	else
		PN = null
